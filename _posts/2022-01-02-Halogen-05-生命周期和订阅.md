---
layout: post
title: Halogen-05-生命周期和订阅
categories:
- 前端 purescript halogen
tags:
- 前端 purescript halogen
author:
  name: purescript-halogen
  link: https://github.com/purescript-halogen/purescript-halogen/blob/master/docs/guide/04-Lifecycles-Subscriptions.md
date: 2022-01-02 11:04 +0800
---

### 生命周期和订阅

到目前为止，您学到的概念涵盖了您将编写的大多数`Halogen`组件。大多数组件都有内部状态，呈现`HTML`元素，并通过在用户单击、悬停或以其他方式与呈现的`HTML`交互时执行操作来做出响应。

但是`actions`也可以从其他类型的事件内部产生。下面是一些常见的例子:

* 您需要在组件启动时运行一个`action`(例如，您需要执行一个`effect`来获得您的初始状态)或者当组件从`DOM`中移除时(例如，清理您获得的资源)。这些被称为生命周期事件。
* 您需要定期运行`action`(例如，您需要每`10`秒执行一次更新),或者当一个事件发生在你渲染的`HTML`之外时(例如，你需要在`DOM`窗口上按下一个键时运行一个`action`, 或者您需要处理在第三方组件(如文本编辑器)中发生的事件)。这些由订阅处理。

当我们在下一章学习父组件和子组件时，我们将学习在组件中产生`action`的另一种方式。本章将重点介绍生命周期和订阅。


#### 生命周期事件

每个`Halogen`组件都可以访问两个生命周期事件:
* 组件可以在初始化时评估一个`action`(`Halogen`创建它)
* 组件可以在完成时评估一个`action`(`Halogen`删除它)

我们指定当组件作为`eval`函数的一部分进行初始化和最终确定时要运行的`action`(如果有的话)，这与我们提供`handleAction`函数的位置相同。在下一节中，我们将更详细地了解`eval`是什么，但首先让我们看一个正在运行的生命周期示例。


下面的示例与我们的随机数组件几乎相同，但有一些重要的变化:

* 除了现有的`Regenerate`操作之外，我们还添加了`Initialize`和`Finalize`。
* 我们已经扩展了我们的`eval`以包含一个`initialize`字段，该字段声明应在组件初始化时评估我们的`Initialize`操作，以及一个`finalize`字段, 该声明应在组件完成时评估我们的`Finalize`操作的。
* 由于我们有两个新`action`，因此我们在`handleAction`函数中添加了两个新`case`来描述如何处理它们。

尝试通读示例:
```purescript
module Main where

import Prelude

import Data.Maybe (Maybe(..), maybe)
import Effect (Effect)
import Effect.Class (class MonadEffect)
import Effect.Class.Console (log)
import Effect.Random (random)
import Halogen as H
import Halogen.Aff as HA
import Halogen.HTML as HH
import Halogen.HTML.Events as HE
import Halogen.VDom.Driver (runUI)

main :: Effect Unit
main = HA.runHalogenAff do
  body <- HA.awaitBody
  runUI component unit body

type State = Maybe Number

data Action
  = Initialize
  | Regenerate
  | Finalize

component :: forall query input output m. MonadEffect m => H.Component query input output m
component =
  H.mkComponent
    { initialState
    , render
    , eval: H.mkEval $ H.defaultEval
        { handleAction = handleAction
        , initialize = Just Initialize
        , finalize = Just Finalize
        }
    }

initialState :: forall input. input -> State
initialState _ = Nothing

render :: forall m. State -> H.ComponentHTML Action () m
render state = do
  let value = maybe "No number generated yet" show state
  HH.div_
    [ HH.h1_
        [ HH.text "Random number" ]
    , HH.p_
        [ HH.text ("Current value: " <> value) ]
    , HH.button
        [ HE.onClick \_ -> Regenerate ]
        [ HH.text "Generate new number" ]
    ]

handleAction :: forall output m. MonadEffect m => Action -> H.HalogenM State Action () output m Unit
handleAction = case _ of
  Initialize -> do
    handleAction Regenerate
    newNumber <- H.get
    log ("Initialized: " <> show newNumber)

  Regenerate -> do
    newNumber <- H.liftEffect random
    H.put (Just newNumber)

  Finalize -> do
    number <- H.get
    log ("Finalized! Last number was: " <> show number)
```

当这个组件挂载时，我们将生成一个随机数并将其记录到控制台。当用户单击按钮时，我们将不断重新生成随机数，当这个组件从`DOM`中移除时，它会记录它在状态中的最后一个数字。


我们在这个例子中做了另一个有趣的改变: 在我们的`Initialize`处理程序中，我们调用了`handleAction Regenerate` -- 我们递归地调用了`handleAction`. 正如我们在这里所做的那样，不时从其他`actions`中调用`actions`会很方便。我们也可以内联`Regenerate`的处理程序 —— 下面的代码做同样的事情:

```purescript
Initialize -> do
    newNumber <- H.liftEffect random
    H.put (Just newNumber)
    log ("Initialized: " <> show newNumber)
```

在我们转向订阅之前，让我们先谈谈`eval`函数。

#### eval函数，mkEval和EvalSpec

我们一直在我们所有的组件中使用`eval`，但到目前为止我们只通过`handleAction`函数处理了由我们的`Halogen HTML`产生的`action`。 但是`eval`函数可以描述我们的组件可以评估`HalogenM`代码以响应事件的所有方式。

在绝大多数情况下，您不需要太关心下面描述的组件规范和评估规范中涉及的所有类型和函数，但我们将简要分解这些类型，以便您了解发生了什么。

`mkComponent`函数接受一个`ComponentSpec`，它是一个包含三个字段的记录:

```purescript
H.mkComponent
  { initialState :: input -> state
  , render :: state -> H.ComponentHTML action slots m
  , eval :: H.HalogenQ query action input ~> H.HalogenM state action slots output m
  }
```

我们已经在`initialState`和`render`函数上花费了大量时间。但是`eval`函数可能看起来很奇怪 —— 什么是`HalogenQ`，`handleAction`之类的函数是如何适应的？现在，我们将重点介绍此函数的最常见用法，但您可以在概念参考中找到完整的详细信息。

`eval`函数描述了如何处理组件中出现的事件。它通常是通过将`mkEval`函数应用于`EvalSpec`来构建的，与我们将`mkComponent`应用于`ComponentSpec`以生成`Component`的方式相同。

为方便起见, `Halogen`提供了一个名为`defaultEval`的已经完成的`EvalSpec`，它在组件中出现事件时不执行任何操作。通过使用这个默认值，你可以只覆盖你关心的值，而其余的值可以什么都不做。

以下是我们如何定义目前仅处理`action`的`eval`函数:

```purescript
H.mkComponent
  { initialState
  , render
  , eval: H.mkEval $ H.defaultEval { handleAction = handleAction }
  }

-- 假设我们已经在范围内定义了一个`handleAction`函数...
handleAction = ...
```

注意：`initialState`和`render`使用缩写`record`双关符号设置; 但是，在这种情况下，`handleAction`不能设置为双关语，因为它是`Records`更新的一部分, [Records语言参考](https://github.com/purescript/documentation/blob/master/language/Records.md#record-update)中提供了有关`record`双关语和`record`更新语法的更多信息。


如果需要，您可以覆盖更多字段。例如，如果您需要支持初始化程序，那么您也将覆盖`initialize`字段:
```purescript
H.mkComponent
  { initialState
  , render
  , eval: H.mkEval $ H.defaultEval
      { handleAction = handleAction
      , initialize = Just Initialize
      }
  }
```

让我们快速浏览一下`EvalSpec`的完整类型:
```purescript
type EvalSpec state query action slots input output m =
  { handleAction :: action -> HalogenM state action slots output m Unit
  , handleQuery :: forall a. query a -> HalogenM state action slots output m (Maybe a)
  , initialize :: Maybe action
  , receive :: input -> Maybe action
  , finalize :: Maybe action
  }
```

`EvalSpec`涵盖了组件内部可用的所有类型。幸运的是，您无需在任何地方指定此类型 —— 您只需向`mkEval`提供一条`record`即可. 我们将在下一章介绍`handleQuery`和`receive`函数以及`query`和`output`类型，因为它们只与子组件相关。

由于在正常使用中，您将覆盖`defaultEval`中的特定字段，而不是自己写出整个`eval`规范，让我们也看看`defaultEval`为这些函数中的每一个实现了什么:

```purescript
defaultEval =
  { handleAction: const (pure unit)
  , handleQuery: const (pure Nothing) -- 我们将在介绍子组件时了解这一点
  , receive: const Nothing -- 我们将在介绍子组件时了解这一点
  , finalize: Nothing
  }
```

现在，让我们转到内部事件的另一个常见来源: 订阅。


##### 订阅

有时您需要处理内部出现的事件，这些事件不是来自与您呈现的`Halogen HTML`交互的用户。两个常见的来源是基于时间的`actions`和事件，它们发生在您渲染的元素之外(如浏览器窗口)。

在`Halogen`中，可以使用[halogen-subscriptions](https://github.com/purescript-halogen/purescript-halogen-subscriptions)库手动创建这些类型的事件。`Halogen`组件可以通过提供应该在发射器触发时运行的`action`来订阅 `Emitter`。

您可以使用`halogen-subscriptions`库中的函数订阅事件，但`Halogen`提供了一个特殊的帮助函数，用于订阅`DOM`中称为`eventListener`的事件侦听器。

一个`Emitter`产生一个`action`流，只要它保持订阅`emitter`，你的组件就会评估这些`action`。创建一个`emitter`并在组件初始化时订阅它是很常见的，尽管您可以随时订阅或取消订阅一个`emitter`。

让我们看一下订阅的两个示例: 一个基于`Aff`的计时器，它计算自组件`mounted`以来的秒数，以及一个基于事件侦听器的流，它报告文档上的键盘事件。


###### 实现一个Timer

我们的第一个示例将使用基于`Aff`的计时器来每秒递增。
```purescript
module Main where

import Prelude

import Control.Monad.Rec.Class (forever)
import Data.Maybe (Maybe(..))
import Effect (Effect)
import Effect.Aff (Milliseconds(..))
import Effect.Aff as Aff
import Effect.Aff.Class (class MonadAff)
import Effect.Exception (error)
import Halogen as H
import Halogen.Aff as HA
import Halogen.HTML as HH
import Halogen.Subscription as HS
import Halogen.VDom.Driver (runUI)

main :: Effect Unit
main = HA.runHalogenAff do
  body <- HA.awaitBody
  runUI component unit body

data Action = Initialize | Tick

type State = Int

component :: forall query input output m. MonadAff m => H.Component query input output m
component =
  H.mkComponent
    { initialState
    , render
    , eval: H.mkEval $ H.defaultEval
        { handleAction = handleAction
        , initialize = Just Initialize
        }
    }

initialState :: forall input. input -> State
initialState _ = 0

render :: forall m. State -> H.ComponentHTML Action () m
render seconds = HH.text ("You have been here for " <> show seconds <> " seconds")

handleAction :: forall output m. MonadAff m => Action -> H.HalogenM State Action () output m Unit
handleAction = case _ of
  Initialize -> do
    _ <- H.subscribe =<< timer Tick
    pure unit

  Tick ->
    H.modify_ \state -> state + 1

timer :: forall m a. MonadAff m => a -> m (HS.Emitter a)
timer val = do
  { emitter, listener } <- H.liftEffect HS.create
  _ <- H.liftAff $ Aff.forkAff $ forever do
    Aff.delay $ Milliseconds 1000.0
    H.liftEffect $ HS.notify listener val
  pure emitter
```

几乎所有这些代码都应该看起来很熟悉，但有两个新部分: 

首先，我们定义了一个可重用的`Emitter`，它将每秒广播一个我们选择的值，直到它没有订阅者:

```purescript
timer :: forall m a. MonadAff m => a -> m (HS.Emitter a)
timer val = do
  { emitter, listener } <- H.liftEffect HS.create
  _ <- H.liftAff $ Aff.forkAff $ forever do
    Aff.delay $ Milliseconds 1000.0
    H.liftEffect $ HS.notify listener val
  pure emitter
```

除非您正在创建与`DOM`中的事件侦听器相关联的`emitters`，否则您应该使用`halal-subscriptions`库中的函数。大多数情况下，您将使用`HS.create`创建一个`emitter`和一个`listener`，但如果您需要手动控制取消订阅，您也可以使用`HS.makeEmitter`。


其次，我们使用`Halogen`的`subscribe`函数附加到`emitter`，同时提供我们想要每秒发射的特定`action`:

```purescript
Initialize -> do
    _ <- H.subscribe =<< timer Tick
    pure unit
```


`subscribe`函数将一个`Emitter`作为参数，并返回一个`SubscriptionId`。您可以随时将此`SubscriptionId`传递给`Halogen`取消订阅功能以结束订阅. 组件在完成时会自动结束其拥有的任何订阅，因此无需在此处取消订阅。

您可能还对[Ace编辑器示例](https://github.com/purescript-halogen/purescript-halogen/tree/master/examples/ace)感兴趣，该示例订阅在第三方`JavaScript`组件中发生的事件，并使用它们来触发`Halogen`组件中的`action`。

###### 使用事件监听器作为订阅

使用订阅的另一个常见原因是当您需要对`DOM`中的事件做出反应时，这些事件不是直接由您控制的`HTML`元素产生的。例如，我们可能想要监听发生在`document`本身上的事件。

在以下示例中，我们订阅`document`上的按键事件，保存按住`Shift`键时输入的任何字符，并在用户按下`Enter`键时停止监听。它演示了使用`eventListener`函数附加事件侦听器并使用`H.unsubscribe`函数选择何时清理它。

在`examples`目录下也有对应的[键盘输入的例子](https://github.com/purescript-halogen/purescript-halogen/tree/master/examples/keyboard-input)。

```purescript
module Main where

import Prelude

import Data.Maybe (Maybe(..))
import Data.String as String
import Effect (Effect)
import Effect.Aff.Class (class MonadAff)
import Halogen as H
import Halogen.Aff as HA
import Halogen.HTML as HH
import Halogen.Query.Event (eventListener)
import Halogen.VDom.Driver (runUI)
import Web.Event.Event as E
import Web.HTML (window)
import Web.HTML.HTMLDocument as HTMLDocument
import Web.HTML.Window (document)
import Web.UIEvent.KeyboardEvent as KE
import Web.UIEvent.KeyboardEvent.EventTypes as KET

main :: Effect Unit
main = HA.runHalogenAff do
  body <- HA.awaitBody
  runUI component unit body

type State = { chars :: String }

data Action
  = Initialize
  | HandleKey H.SubscriptionId KE.KeyboardEvent

component :: forall query input output m. MonadAff m => H.Component query input output m
component =
  H.mkComponent
    { initialState
    , render
    , eval: H.mkEval $ H.defaultEval
        { handleAction = handleAction
        , initialize = Just Initialize
        }
    }

initialState :: forall input. input -> State
initialState _ = { chars: "" }

render :: forall m. State -> H.ComponentHTML Action () m
render state =
  HH.div_
    [ HH.p_ [ HH.text "Hold down the shift key and type some characters!" ]
    , HH.p_ [ HH.text "Press ENTER or RETURN to clear and remove the event listener." ]
    , HH.p_ [ HH.text state.chars ]
    ]

handleAction :: forall output m. MonadAff m => Action -> H.HalogenM State Action () output m Unit
handleAction = case _ of
  Initialize -> do
    document <- H.liftEffect $ document =<< window
    H.subscribe' \sid ->
      eventListener
        KET.keyup
        (HTMLDocument.toEventTarget document)
        (map (HandleKey sid) <<< KE.fromEvent)

  HandleKey sid ev
    | KE.shiftKey ev -> do
        H.liftEffect $ E.preventDefault $ KE.toEvent ev
        let char = KE.key ev
        when (String.length char == 1) do
          H.modify_ \st -> st { chars = st.chars <> char }

    | KE.key ev == "Enter" -> do
        H.liftEffect $ E.preventDefault (KE.toEvent ev)
        H.modify_ _ { chars = "" }
        H.unsubscribe sid

    | otherwise ->
        pure unit
```

在这个例子中，我们使用了`H.subscribe'`函数，它将`SubscriptionId`传递给`emitter`而不是返回它。这是一种替代方法，可以让您将`ID`保留在`action`类型而不是`state`中，这样会更方便。

我们将`emitter`直接写入我们的代码中以处理`Initialize`操作，该操作在`document`上注册一个事件侦听器并在每次按下键时发出`HandleKey`。

`eventListener`使用`purescript-web`库中的类型来处理`DOM`以手动构建事件侦听器:

```purescript
eventListener
  :: forall a
   . Web.Event.EventType
  -> Web.Event.EventTarget.EventTarget
  -> (Web.Event.Event -> Maybe a)
  -> HS.Emitter a
```

它需要一种要监听的事件类型(在我们的例子中:`keyup`)，一个指示在哪里监听事件的目标(在我们的例子中: `HTMLDocument`本身)，以及一个将发生的事件转换为应该发出的类型的回调函数(在我们的例子中: 我们通过在`HandleKey`构造函数中捕获事件来发出我们的`Action`类型).

##### Wrapping Up

`Halogen`组件使用`Action`类型来处理组件内部出现的各种事件。我们现在已经看到了这种情况可能发生的所有常见方式:
* 用户与我们呈现的`HTML`元素的交互
* 生命周期事件
* 订阅，无论是通过`Aff`和`Effect`函数还是来自`DOM`上的事件侦听器

您现在已经了解了单独使用`Halogen`组件的所有基本知识。在下一章中，我们将学习如何将`Halogen`组件组合成父组件和子组件的树。


